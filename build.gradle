apply plugin: 'java-library'

project.version = '2.0.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7

// if we are not on travis, use the publishing file
if (!isTravis()) {
    apply from: 'gradle/publishing.gradle'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    
    group 'me.xdrop'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
    }

    dependencies {
        testImplementation 'org.codehaus.groovy:groovy-test:2.5.3'
    }
    
    jar {
        // add a redundant classifier to the jars; important for adding artifacts to publication
        classifier "sources-${project.name}".toLowerCase()
    }

    javadoc {
        source = sourceSets.main.allJava
        options.encoding = 'UTF-8'
        options.destinationDirectory = new File("docs\\javadoc")
    }

    compileTestGroovy {
        // DefaultStringProcessorTest is using special characters
        options.encoding = 'UTF-8'
    }

    sourceSets {
        main {
            java {
                srcDirs = ["src\\main\\java"]
            }
        }

        test {
            java {
                srcDirs = ["src\\test\\java"]
            }
        }
    }
}

project(':fuzzywuzzy') {
    version '2.0.0'

    dependencies {
        implementation project(':diffutils')
        testImplementation 'org.easymock:easymock:3.6'
    }
}

project(':diffutils') {
    version '1.3'
}

// utility method to decide whether we are on travis ci machine
static boolean isTravis() {
    return System.getenv().containsKey("TRAVIS")
}