import java.util.function.BiConsumer

apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    repositories {
        maven {
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = project.version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            name = "Sonatype"

            credentials {
                def uName = ""
                def passwd = ""
                int r
                def stream = new FileInputStream(new File(".credentials/sonatype-username.cred"))
                while ((r = stream.read()) != -1) uName = (uName + (char)r)
                stream = new FileInputStream(new File(".credentials/sonatype-password.cred"))
                while ((r = stream.read()) != -1) passwd = (passwd + (char)r)

                username uName
                password passwd
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'fuzzywuzzy'
            from components.java

            gradle.rootProject.getChildProjects().forEach(new BiConsumer<? super String, ? super Project>(){
                @Override
                void accept(Object o1, Object o2) {
                    final String name = (String) o1
                    final Project proj = (Project) o2
                    if (!name.toLowerCase().contains("compat")) // if not a compat module
                        gradle.rootProject.publishing.publications.mavenJava.artifact proj.jar
                }
            })

            pom {
                name = "FuzzyWuzzy"
                description = "Fuzzy string matching algorithm for Java"
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'GPL 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id = "xdrop"
                        name = "Panayiotis P"
                        url = "http://www.github.com/xdrop"
                        timezone = "0"
                    }
                }

                contributors {
                    contributor {
                        id = "burdoto"
                        name = "Tobias Burdow"
                        url = "http://www.github.com/burdoto"
                        timezone = "+1"
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/xdrop/fuzzywuzzy.git'
                    developerConnection = 'scm:git:git@github.com:xdrop/fuzzywuzzy.git'
                    url = 'https://github.com/xdrop/fuzzywuzzy'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/xdrop/fuzzywuzzy/issues'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    //noinspection GroovyAssignabilityCheck -> Wrong Error
    sign publishing.publications.mavenJava
}